plugins {
    id 'com.android.application'
    id 'com.google.android.gms.oss-licenses-plugin'
}
android {
    compileSdkVersion 36
    buildToolsVersion "36.0.0"

    defaultConfig {
        applicationId "io.github.danthe1st.jdoc4droid"
        minSdkVersion 26
        targetSdkVersion 36
        versionCode Integer.parseInt(providers.systemProperty('versionCode').forUseAtConfigurationTime().getOrElse("1"))
        versionName providers.systemProperty('versionName').forUseAtConfigurationTime().getOrElse("1.0")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    if (System.getenv("KEYSTORE") != null) {
        logger.info("signing enabled")
        signingConfigs {
            release {
                storeFile file(System.getenv("KEYSTORE"))
                storePassword System.getenv("KEYSTORE_PWD")
                keyAlias System.getenv("SIGNING_KEY")
                keyPassword System.getenv("SIGNING_KEY_PWD")
                v1SigningEnabled true
                v2SigningEnabled true
            }
        }
    } else {
        logger.info("signing disabled")
    }

    buildTypes {
        release {
            if (System.getenv("KEYSTORE") != null) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }
    namespace 'io.github.danthe1st.jdoc4droid'
    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
    }
}


dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'org.jsoup:jsoup:1.21.1'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.2.1'
    implementation "androidx.activity:activity:1.10.1"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    implementation 'androidx.webkit:webkit:1.14.0'
}
