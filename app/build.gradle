plugins {
    id 'com.android.application'
    id 'com.google.android.gms.oss-licenses-plugin'
}
repositories {
    maven {
        url "https://maven.mozilla.org/maven2/"
    }
}
android {
    compileSdkVersion 35
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "io.github.danthe1st.jdoc4droid"
        minSdkVersion 26
        targetSdkVersion 35
        versionCode Integer.parseInt(providers.systemProperty('versionCode').forUseAtConfigurationTime().getOrElse("1"))
        versionName providers.systemProperty('versionName').forUseAtConfigurationTime().getOrElse("1.0")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    if (System.getenv("KEYSTORE") != null) {
        logger.info("signing enabled")
        signingConfigs {
            release {
                storeFile file(System.getenv("KEYSTORE"))
                storePassword System.getenv("KEYSTORE_PWD")
                keyAlias System.getenv("SIGNING_KEY")
                keyPassword System.getenv("SIGNING_KEY_PWD")
                v1SigningEnabled true
                v2SigningEnabled true
            }
        }
    } else {
        logger.info("signing disabled")
    }

    buildTypes {
        release {
            if (System.getenv("KEYSTORE") != null) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }
    namespace 'io.github.danthe1st.jdoc4droid'
    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
    }
    lint {
        //https://stackoverflow.com/a/76011953/10871900
        // https://github.com/google/ExoPlayer/issues/10884
        lintConfig = file("$rootDir/lint_config.xml")
    }
}


dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'org.jsoup:jsoup:1.14.3'
    implementation "org.mozilla.geckoview:geckoview:133.0.20241209150345"
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.1.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}
